---
title: Assembly Language Lab
top: false
cover: false
img: 'http://bucket.jiabi.tech/medias/featureimages/asm.jpg'
toc: true
mathjax: true
date: 2020-04-13 01:32:21
password:
summary: è®°å½•ç‹çˆ½æ±‡ç¼–è¯­è¨€ç¬¬ä¸‰ç‰ˆå®éªŒç›¸å…³
tags:
    - ASM
    - LAB
categories:
    - è®¡ç®—æœºåŸºç¡€
---

# ç‹çˆ½ã€Šæ±‡ç¼–è¯­è¨€ã€‹ç¬¬ä¸‰ç‰ˆ å®éªŒ

> ä»¥åç”¨åˆ°çš„çŸ¥è¯†ï¼Œæˆ‘ä»¬ä»¥åå†è¯´ã€‚

> ä¹…é—»å¤§åï¼Œæœªæ›¾æ‹œè¯»  
    è¿™æ¬¡è¶ç€å¾®æœºè¯¾ç¨‹ï¼Œè§£å»å¿ƒå¤´ä¹‹ç—’  
    é¡ºä¾¿åšå®¢åˆšå¼€ï¼Œå°±éšæ‰‹è®°å½•ä¸‹ä¹¦æœ¬ä¸Šçš„å®éªŒ  

## ç¯å¢ƒ

```bash
C:\>ver 
MS-DOS Version 6.22  
C:\MASM>masm
Microsoft (R) MASM Compatibility Driver Version 6.11  
Copyright (C) Microsoft Corp 1993.  A1l rights reserved.
```

## å®éªŒ 1 æŸ¥çœ‹CPUå’Œå†…å­˜ï¼Œç”¨æœºå™¨æŒ‡ä»¤å’Œæ±‡ç¼–æŒ‡ä»¤ç¼–ç¨‹  

1. ä½¿ç”¨Debugï¼Œå°†ä¸‹é¢çš„ç¨‹åºæ®µå†™å…¥å†…å­˜ï¼Œé€æ¡æ‰§è¡Œï¼Œè§‚å¯Ÿæ¯æ¡æŒ‡ä»¤æ‰§è¡Œåï¼ŒCPUä¸­ç›¸å…³å¯„å­˜å™¨çš„å˜åŒ–ã€‚  
    æç¤ºï¼Œå¯ç”¨Eå‘½ä»¤å’ŒAå‘½ä»¤ä»¥ä¸¤ç§æ–¹å¼å°†æŒ‡ä»¤å†™å…¥å†…å­˜ã€‚æ³¨æ„ç”¨Tå‘½ä»¤æ‰§è¡Œæ—¶ï¼ŒCS:IPçš„æŒ‡å‘ã€‚  
    æ³¨ï¼šè¿™é‡Œç•¥å»å…¶ä»–å¯„å­˜å™¨çš„å˜åŒ–
    ```x86asm
    mov ax,4e20h    ;ax = 4e20h
    add ax,1416h    ;ax = 6236h
    mov bx,2000h    ;bx = 2000h
    add ax,bx       ;ax = 8236h
    mov bx,ax       ;bx = 8236h
    add ax,bx       ;ax = 046ch
    mov ax,001ah    ;ax = 001ah
    mov bx,0026h    ;bx = 0026h
    add al,bl       ;ax = 0040h
    add ah,bl       ;ax = 2640h
    add bh,al       ;bx = 4026h
    mov ah,0        ;ax = 0040h
    add al,bl       ;ax = 0066h
    add al,9ch      ;ax = 0002h
    ```
    
2. å°†ä¸‹é¢3æ¡æŒ‡ä»¤å†™å…¥ä»2000:0å¼€å§‹çš„å†…å­˜å•å…ƒä¸­ï¼Œåˆ©ç”¨è¿™3æ¡æŒ‡ä»¤è®¡ç®—2çš„8æ¬¡æ–¹ã€‚  
    ```x86asm
    ;2^8 = 256 = 100h
    mov ax, 1
    add ax, ax
    jmp 2000:0003
    ```
    æ¯æ¬¡æ‰§è¡Œ add ax,ax ç›¸å½“äºå°†axä¹˜2ï¼Œé‡å¤æ‰§è¡Œè¯¥æ¡æŒ‡ä»¤8æ¬¡å³å¯ã€‚

3. æŸ¥çœ‹å†…å­˜ä¸­çš„å†…å®¹ã€‚  
    PCæœºä¸»æ¿ä¸Šçš„ROMå†™æœ‰ä¸€ä¸ªç”Ÿäº§æ—¥æœŸï¼Œåœ¨å†…å­˜FFF00Hï½FFFFFHçš„æŸå‡ ä¸ªå•å…ƒä¸­ï¼Œè¯·æ‰¾åˆ°è¿™ä¸ªç”Ÿäº§æ—¥æœŸå¹¶è¯•å›¾æ”¹å˜å®ƒã€‚  
    æç¤ºï¼Œå¦‚æœè¯»è€…å¯¹å®éªŒçš„ç»“æœæ„Ÿåˆ°ç–‘æƒ‘ï¼Œè¯·ä»”ç»†é˜…è¯»ç¬¬1ç« ä¸­çš„1.15èŠ‚ã€‚  
    ç»“æœå¦‚å›¾ï¼š04/13/18ï¼Œåªè¯»ä¸å¯æ”¹å†™  
    ![LAB1_3 Result](http://bucket.jiabi.tech/images/asmLab/lab1_3.jpg)

4. å‘å†…å­˜ä»B8100Hå¼€å§‹çš„å•å…ƒä¸­å¡«å†™æ•°æ®ï¼Œå¦‚ï¼š  
    ```bash
    -e B810:0000 01 01 02 02 03 03 04 04
    ```
    è¯·è¯»è€…å…ˆå¡«å†™ä¸åŒçš„æ•°æ®ï¼Œè§‚å¯Ÿäº§ç”Ÿçš„ç°è±¡ï¼›å†æ”¹å˜å¡«å†™çš„åœ°å€ï¼Œè§‚å¯Ÿäº§ç”Ÿçš„ç°è±¡ã€‚  
    æç¤ºï¼Œå¦‚æœè¯»è€…å¯¹å®éªŒçš„ç»“æœæ„Ÿåˆ°ç–‘æƒ‘ï¼Œè¯·ä»”ç»†é˜…è¯»ç¬¬1ç« ä¸­çš„1.15èŠ‚ã€‚  
    æ³¨ï¼šæ˜¾å­˜åŒºåŸŸï¼Œç»“æœç•¥

## å®éªŒ 2 ç”¨æœºå™¨æŒ‡ä»¤å’Œæ±‡ç¼–æŒ‡ä»¤ç¼–ç¨‹

1. ä½¿ç”¨Debugï¼Œå°†ä¸‹é¢çš„ç¨‹åºæ®µå†™å…¥å†…å­˜ï¼Œé€æ¡æ‰§è¡Œï¼Œæ ¹æ®æŒ‡ä»¤æ‰§è¡Œåçš„å®éªŒè¿è¡Œæƒ…å†µå¡«ç©ºã€‚  
    æ³¨ï¼šç»“æœä¸å”¯ä¸€
    ```x86asm
    mov ax,ffff
    mov ds,ax

    mov ax,2200
    mov ss,ax

    mov sp,0100

    mov ax,[0]  ;ax = 5BEA
    add ax,[2]  ;ax = 5CCA
    mov bx,[4]  ;bx = 30F0
    add bx,[6]  ;bx = 6024

    push ax     ;sp = 00FE; ä¿®æ”¹çš„å†…å­˜å•å…ƒçš„åœ°å€æ˜¯ 220FE å†…å®¹ä¸º 5CCA
    push bx     ;sp = 00FC; ä¿®æ”¹çš„å†…å­˜å•å…ƒçš„åœ°å€æ˜¯ 220FC å†…å®¹ä¸º 6024
    pop ax      ;sp = 00FE; ax = 6024
    pop bx      ;sp = 0100; bx = 5CCA

    push [4]    ;sp = 00FE; ä¿®æ”¹çš„å†…å­˜å•å…ƒçš„åœ°å€æ˜¯ 220FE å†…å®¹ä¸º 30F0
    push [6]    ;sp = 00FC; ä¿®æ”¹çš„å†…å­˜å•å…ƒçš„åœ°å€æ˜¯ 220FC å†…å®¹ä¸º 2F34
    ```
2. ä»”ç»†è§‚å¯Ÿå›¾3.19ä¸­çš„å®éªŒè¿‡ç¨‹ï¼Œç„¶ååˆ†æï¼šä¸ºä»€ä¹ˆ2000:0ï½2000:fä¸­çš„å†…å®¹ä¼šå‘ç”Ÿæ”¹å˜ï¼Ÿ  
    ![IMG 3.19](http://bucket.jiabi.tech/images/asmLab/img3.19.jpg)  
    åœ¨ä½¿ç”¨Tå‘½ä»¤è¿›è¡Œå•æ­¥è¿½è¸ªçš„æ—¶å€™ï¼Œäº§ç”Ÿäº†ä¸­æ–­ï¼ŒCSå’ŒIPç­‰å¯„å­˜å™¨ä¾æ­¤å…¥æ ˆï¼Œå¯¼è‡´äº†å†…å­˜ç›¸å…³ä½ç½®å†…å®¹çš„æ”¹å˜ã€‚

## å®éªŒ 3 ç¼–ç¨‹ã€ç¼–è¯‘ã€è¿æ¥ã€è·Ÿè¸ª

1. å°†ä¸‹é¢çš„ç¨‹åºä¿å­˜ä¸º t1.asm æ–‡ä»¶ï¼Œå°†å…¶ç”Ÿæˆå¯æ‰§è¡Œæ–‡ä»¶ t1.exeã€‚
    ```x86asm
    assume cs:codeseg

    codeseg segment
            mov ax,2000H
            mov ss,ax
            mov sp,0
            add sp,10
            pop ax
            pop bx
            push ax
            push bx
            pop ax
            pop bx

            mov ax,4c00H
            int 21H

    codeseg ends

    end
    ```

2. ç”¨Debugè·Ÿè¸ª t1.exe çš„æ‰§è¡Œè¿‡ç¨‹ï¼Œå†™å‡ºæ¯ä¸€æ­¥æ‰§è¡Œåï¼Œç›¸å…³å¯„å­˜å™¨ä¸­çš„å†…å®¹å’Œæ ˆé¡¶çš„å†…å®¹ã€‚  
    ç»“æœç•¥
3. PSPçš„å¤´ä¸¤ä¸ªå­—èŠ‚æ˜¯CD 20ï¼Œç”¨DebugåŠ è½½ t1.exeï¼ŒæŸ¥çœ‹PSPçš„å†…å®¹ã€‚  
    æ³¨ï¼šè¿™é‡Œåº”è¯¥æ˜¯"-u 0A67:0"  
        å³ 0A67 = CS-0010h  
    ![PSP](http://bucket.jiabi.tech/images/asmLab/lab3_result_u_ds.jpg)

## å®éªŒ 4 [bx]å’Œloopçš„ä½¿ç”¨

1. ç¼–ç¨‹ï¼Œå‘å†…å­˜0:200ï½0:23Fä¾æ¬¡ä¼ é€æ•°æ®0ï½63(3FH)ã€‚  
2. ç¼–ç¨‹ï¼Œå‘å†…å­˜0:200ï½0:23Fä¾æ­¤ä¼ é€æ•°æ®0ï½63(3FH)ï¼Œç¨‹åºä¸­åªèƒ½ä½¿ç”¨9æ¡æŒ‡ä»¤ï¼Œ9æ¡æŒ‡ä»¤ä¸­åŒ…æ‹¬"mov ax,4c00h"å’Œ"int 21h"ã€‚  
    ```x86asm
    assume cs:code

    code segment
        mov ax,0020h
        mov ds,ax
        mov bx,0

        mov cx,40h
        s:
        mov [bx],bx
        inc bx
        loop s

        mov ax,4c00h
        int 21h
    code ends

    end
    ```
3. ä¸‹é¢çš„ç¨‹åºçš„åŠŸèƒ½æ˜¯å°†"mov ax, 4c00h"ä¹‹å‰çš„æŒ‡ä»¤å¤åˆ¶åˆ°å†…å­˜çš„0:200å¤„ï¼Œè¡¥å…¨ç¨‹åºï¼Œä¸Šæœºè°ƒè¯•ï¼Œè·Ÿè¸ªè¿è¡Œç»“æœã€‚  
    ```x86asm
    assume cs:code

    code segment
        mov ax,cs
        mov ds,ax

        mov ax,0020h
        mov es,ax

        mov bx,0
        mov cx,0017h
        s:
        mov al,[bx]
        mov es:[bx],al
        inc bx
        loop s

        mov ax,4c00h
        int 21h
    code ends

    end
    ```
    æç¤ºï¼š  
    - å¤åˆ¶çš„æ˜¯ä»€ä¹ˆï¼Ÿä»å“ªé‡Œåˆ°å“ªé‡Œï¼Ÿ  
        å¤åˆ¶çš„æ˜¯æ­¤ç¨‹åºä¸­ï¼Œä»CS:0åˆ°"mov ax,4c00h"é¦–åœ°å€ä¹‹é—´çš„ä»£ç ï¼Œä»¥å­—èŠ‚ä¸ºå•ä½
    - å¤åˆ¶çš„æ˜¯ä»€ä¹ˆï¼Ÿæœ‰å¤šå°‘ä¸ªå­—èŠ‚ï¼Ÿä½ å¦‚ä½•çŸ¥é“è¦å¤åˆ¶çš„å­—èŠ‚çš„æ•°é‡ï¼Ÿ  
        cs:0--cs:17h = 18h = 24 ä¸ªå­—èŠ‚  
        æ³¨æ„ï¼šåœ°å€æ˜¯ä»0å¼€å§‹çš„  
        Debugä¸­ä½¿ç”¨"u cs:0"è§‚å¯Ÿå¾—å‡º  
        å‚è€ƒï¼šå¯ä»¥ä½¿ç”¨offsetè®¡ç®—

## å®éªŒ 5 ç¼–å†™ã€è°ƒè¯•å…·æœ‰å¤šä¸ªæ®µçš„ç¨‹åº

1. å°†ä¸‹é¢çš„ç¨‹åºç¼–è¯‘ã€è¿æ¥ï¼Œç”¨DebugåŠ è½½ã€è·Ÿè¸ªï¼Œç„¶åå›ç­”é—®é¢˜ã€‚
    ```x86asm
    assume cs:code,ds:data,ss:stack

    data segment
        dw 0123h,0456h,0789h,0abch,0defh,0fedh,0cbah,0987h
    data ends

    stack segment
        dw 0,0,0,0,0,0,0,0
    stack ends

    code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,10h

        mov ax,data
        mov ds,ax

        push ds:[0]
        push ds:[2]
        pop ds:[2]
        pop ds:[0]

        mov ax,4c00h
        int 21h
    code ends

    end start
    ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼Œdataæ®µä¸­çš„æ•°æ®ä¸ºå¤šå°‘ï¼Ÿ  
        dataæ®µä¸­çš„æ•°æ®ä¸å˜  
        ```
        -d ds:0 f
        0A77:0000 23 01 56 04 89 07 BC 0A-EF 0D ED 0F BA 0C 87 09
        ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼ŒCS = 0A79 ã€SS = 0A78 ã€DS = 0A77ã€‚(ç­”æ¡ˆä¸å”¯ä¸€)
    - è®¾ç¨‹åºåŠ è½½åï¼Œcodeæ®µçš„æ®µåœ°å€ä¸ºXï¼Œåˆ™dataæ®µçš„æ®µåœ°å€ä¸º X-2 ï¼Œstackæ®µçš„æ®µåœ°å€ä¸º X-1 ã€‚

2. å°†ä¸‹é¢çš„ç¨‹åºç¼–è¯‘ã€è¿æ¥ï¼Œç”¨DebugåŠ è½½ã€è·Ÿè¸ªï¼Œç„¶åå›ç­”é—®é¢˜ã€‚  
    ```x86asm
    assume cs:code,ds:data,ss:stack

    data segment
        dw 0123h,0456h
    data ends

    stack segment
        dw 0,0
    stack ends

    code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,10h

        mov ax,data
        mov ds,ax

        push ds:[0]
        push ds:[2]

        pop ds:[2]
        pop ds:[0]

        mov ax,4c00h
        int 21h
    code ends

    end start
    ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼Œdataæ®µä¸­çš„æ•°æ®ä¸ºå¤šå°‘ï¼Ÿ  
        dataæ®µä¸­çš„æ•°æ®ä¸å˜
        ```
        -d ds:0 f
        0A77:0000 23 01 56 04 00 00 00 00-00 00 00 00 00 00 00 00
        ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼ŒCS = 0A79 ã€SS = 0A78 ã€DS = 0A77 ã€‚(ç­”æ¡ˆä¸å”¯ä¸€)
    - è®¾ç¨‹åºåŠ è½½åï¼Œcodeæ®µçš„æ®µåœ°å€ä¸ºXï¼Œåˆ™dataæ®µçš„æ®µåœ°å€ä¸º X-2 ï¼Œstackæ®µçš„æ®µåœ°å€ä¸º X-1 ã€‚
    - å¯¹äºå¦‚ä¸‹å®šä¹‰çš„æ®µï¼š
        ```x86asm
        name segment
            ...
        name ends
        ```
        å¦‚æœæ®µä¸­çš„æ•°æ®å Nä¸ªå­—èŠ‚ï¼Œåˆ™ç¨‹åºåŠ è½½åï¼Œè¯¥æ®µå®é™…å æœ‰çš„ç©ºé—´ä¸º ((N-1)/16+1)*16 å­—èŠ‚ã€‚  
        æ³¨ï¼š"(N-1)/16+1"ä¸ºæ•´é™¤,å³"N/16"å¹¶å‘ä¸Šå–æ•´ï¼šä»¥16å­—èŠ‚(Paragraph)å¯¹é½

3. å°†ä¸‹é¢çš„ç¨‹åºç¼–è¯‘ã€è¿æ¥ï¼Œç”¨DebugåŠ è½½ã€è·Ÿè¸ªï¼Œç„¶åå›ç­”é—®é¢˜ã€‚  
    ```x86asm
    assume cs:code,ds:data,ss:stack

    code segment
    start:mov ax,stack
        mov ss,ax
        mov sp,10h

        mov ax,data
        mov ds,ax

        push ds:[0]
        push ds:[2]
        pop ds:[2]
        pop ds:[0]

        mov ax,4c00h
        int 21h
    code ends

    data segment
        dw 0123h,0456h
    data ends

    stack segment
        dw 0,0
    stack ends

    end start
    ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼Œdataæ®µä¸­çš„æ•°æ®ä¸ºå¤šå°‘ï¼Ÿ  
        dataæ®µä¸­çš„æ•°æ®ä¸å˜
        ```
        -d ds:0 f
        0A7A:0000 23 01 56 04 00 00 00 00-00 00 00 00 00 00 00 00
        ```
    - CPUæ‰§è¡Œç¨‹åºï¼Œç¨‹åºè¿”å›å‰ï¼ŒCS = 0A77 ã€SS = 0A7B ã€DS = 0A7A ã€‚(ç­”æ¡ˆä¸å”¯ä¸€)
    - è®¾ç¨‹åºåŠ è½½åï¼Œcodeæ®µçš„æ®µåœ°å€ä¸ºXï¼Œåˆ™dataæ®µçš„æ®µåœ°å€ä¸º X+3 ï¼Œstackæ®µçš„æ®µåœ°å€ä¸º X+4 ã€‚

4. å¦‚æœå°†1ã€2ã€3é¢˜ä¸­çš„æœ€åä¸€æ¡ä¼ªæŒ‡ä»¤"end start"æ”¹ä¸º"end"ï¼ˆä¹Ÿå°±æ˜¯è¯´ï¼Œä¸æŒ‡æ˜ç¨‹åºçš„å…¥å£ï¼‰ï¼Œåˆ™å“ªä¸ªç¨‹åºä»ç„¶å¯ä»¥æ­£ç¡®æ‰§è¡Œï¼Ÿè¯·è¯´æ˜åŸå› ã€‚  
    åªæœ‰é¢˜3ä¸­ç¨‹åºå¯ä»¥æ­£ç¡®è¿è¡Œï¼Œåœ¨ä¸æŒ‡æ˜ç¨‹åºå…¥å£çš„æƒ…å†µä¸‹ï¼Œç¨‹åºé»˜è®¤æŒ‰ç…§é¡ºåºä»å¤´å¼€å§‹æ‰§è¡Œã€‚  
    è€Œ3ä¸ªç¨‹åºä¸­åªæœ‰é¢˜3ä¸­ç¨‹åºçš„codeæ®µä½äºæœ€å¼€å§‹çš„éƒ¨åˆ†ï¼Œæ‰€ä»¥åªæœ‰é¢˜3ä¸­ç¨‹åºå¯ä»¥æ­£ç¡®è¿è¡Œã€‚  

5. ç¨‹åºå¦‚ä¸‹ï¼Œç¼–å†™codeæ®µä¸­çš„ä»£ç ï¼Œå°†aæ®µå’Œbæ®µä¸­çš„æ•°æ®ä¾æ­¤ç›¸åŠ ï¼Œå°†ç»“æœå­˜åˆ°cæ®µä¸­ã€‚  
    ```x86asm
    assume cs:code
    a segment
        db 1,2,3,4,5,6,7,8
    a ends

    b segment
        db 1,2,3,4,5,6,7,8
    b ends

    c segment
        db 0,0,0,0,0,0,0,0
    c ends

    code segment
    start:
            ?
    code ends

    end start
    ```
    æ³¨ï¼šåœ¨åšä¸»çš„[DOSç¯å¢ƒä¸MASMç‰ˆæœ¬](#toc-heading-1)ä¸‹ï¼Œ"c"æ˜¯ä¿ç•™å…³é”®å­—ï¼Œæ•…æ”¹å†™ä¸º"cc segment"
    ```x86asm
    assume cs:code

    a segment
    db 1,2,3,4,5,6,7,8
    a ends

    b segment
    db 1,2,3,4,5,6,7,8
    b ends
    
    cc segment
    db 0,0,0,0,0,0,0,0 
    cc ends

    code segment
    start:mov ax,cc
        mov ds,ax

        mov bx,0
        mov cx,8
        s:
        mov dl,0
        mov ax,a
        mov es,ax
        add dl,es:[bx]
        mov ax,b
        mov es,ax
        add dl,es:[bx]
        mov [bx],dl
        inc bx
        loop s

        mov ax,4c00h
        int 21h
    code ends

    end start
    ```

6. ç¨‹åºå¦‚ä¸‹ï¼Œç¼–å†™codeæ®µä¸­çš„ä»£ç ï¼Œç”¨pushæŒ‡ä»¤å°†aæ®µä¸­çš„å‰8ä¸ª**å­—å‹**æ•°æ®ï¼Œé€†åºå­˜å‚¨åˆ°bæ®µä¸­ã€‚  
    ```x86asm
    assume cs:code

    a segment
        dw 1,2,3,4,5,6,7,8,9,0ah,0bh,0ch,0dh,0eh,0fh,0ffh
    a ends

    b segment
        dw 0,0,0,0,0,0,0,0
    b ends

    code segment
    start:  
            ?
    code ends

    end start
    ```
    æ³¨ï¼šè¿›è¡Œæ ˆæ“ä½œæ—¶ï¼Œæ³¨æ„æ•°æ®çš„å¤§å°
    ```x86asm
    assume cs:code

    a segment
    dw 1,2,3,4,5,6,7,8,9,0ah,0bh,0ch,0dh,0eh,0fh,0ffh
    a ends

    b segment
    dw 0,0,0,0,0,0,0,0
    b ends

    code segment
    start:
        mov ax,a
        mov ds,ax

        mov ax,b
        mov ss,ax
        mov sp,10h

        mov cx,8
        mov bx,0

        s:
        push [bx]
        add bx,2
        loop s

        mov ax,4c00h
        int 21h
    code ends

    end start
    ```

## å®éªŒ 6 å®è·µè¯¾ç¨‹ä¸­çš„ç¨‹åº

1. å°†è¯¾ç¨‹ä¸­æ‰€æœ‰è®²è§£è¿‡çš„ç¨‹åºä¸Šæœºè°ƒè¯•ï¼Œç”¨Debugè·Ÿè¸ªå…¶æ‰§è¡Œè¿‡ç¨‹ï¼Œå¹¶åœ¨è¿‡ç¨‹ä¸­è¿›ä¸€æ­¥ç†è§£æ‰€è®²å†…å®¹ã€‚  
    ç•¥

2. ç¼–ç¨‹ï¼Œå®Œæˆé—®é¢˜7.9ä¸­çš„ç¨‹åºã€‚
    > é—®é¢˜7.9  
    ç¼–ç¨‹ï¼Œå°† datasg æ®µä¸­æ¯ä¸ªå•è¯çš„å‰å››ä¸ªå­—æ¯æ”¹ä¸ºå¤§å†™å­—æ¯ã€‚  
    ```x86asm
    datasg segment  
        db '1. display      '  
        db '2. brows        '  
        db '3. replace      '  
        db '4. modify       '  
    datasg ends
    ```
    [DOSç¯å¢ƒä¸MASMç‰ˆæœ¬](#toc-heading-1)  
    åœ¨ç¼–å†™è¿™ä¸ªç¨‹åºçš„è¿‡ç¨‹ä¸­ï¼Œé‡åˆ°ä¸€ä¸ªå¥‡æ€ªçš„é—®é¢˜ï¼š 
    ``` x86asm
    ;ç¼–è¯‘é”™è¯¯ error A2070:invalid instruction
    mov al,[bx+3+si]
    ;ç¼–è¯‘é€šè¿‡
    mov al,byte ptr [bx+3+si]
    ;ç¼–è¯‘é€šè¿‡
    mov al,[bx+si+3]
    ;ç¼–è¯‘é€šè¿‡
    mov ax,[bx+3+si]
    ```
    ç®€å•æŸ¥é˜…äº†[MASM6.1æ–‡æ¡£](https://github.com/ChiuJun/CSAPP_3e_Solutions/blob/master/Reference/MASM6.1%E6%96%87%E6%A1%A3.pdf)ï¼Œæ²¡çœ‹å‡ºæ˜¯ä»€ä¹ˆåŸå› å¯¼è‡´ï¼Œä»¥ä¸‹æ˜¯å®Œæ•´çš„ç¨‹åºï¼š
    ```x86asm
    assume cs:codeseg,ss:stackseg,ds:dataseg

    stackseg segment
    dw 0,0,0,0,0,0,0,0
    stackseg ends

    dataseg segment
    db '1. display      '
    db '2. brows        '
    db '3. replace      '
    db '4. modify       '
    dataseg ends

    codeseg segment
    start:
        mov ax,stackseg
        mov ss,ax
        mov sp,10H

        mov ax,dataseg
        mov ds,ax

        mov bx,0
        mov cx,4
    s:
        push cx
        mov si,0
        mov cx,4
        
        i:
        mov al,[bx+si+3]
        and al,11011111B
        mov [bx+si+3],al
        inc si
        loop i

        add bx,10H
        pop cx
    loop s

        mov ax,4c00H
        int 21H
    codeseg ends

    end start
    ```
    ![LAB6_2 Result](http://bucket.jiabi.tech/images/asmLab/lab6_2_result.jpg)

## å®éªŒ 7 å¯»å€æ–¹å¼åœ¨ç»“æ„åŒ–æ•°æ®è®¿é—®ä¸­çš„åº”ç”¨

- ç¼–ç¨‹ï¼Œå°† data æ®µä¸­çš„æ•°æ®æŒ‰å¦‚ä¸‹æ ¼å¼å†™å…¥åˆ° table æ®µä¸­ï¼Œå¹¶è®¡ç®—21å¹´ä¸­çš„äººå‡æ”¶å…¥(å–æ•´)ï¼Œç»“æœä¹ŸæŒ‰ç…§ä¸‹é¢çš„æ ¼å¼ä¿å­˜åœ¨tableæ®µä¸­ã€‚  
    ![LAB7 æ ¼å¼](http://bucket.jiabi.tech/images/asmLab/lab7_format.jpg)  
    ç¼–å†™å®Œä¹‹åï¼Œåšä¸»è§‰å¾—è¿™æ ·çš„å†™æ³•æ›´å®¹æ˜“å‡ºé”™ï¼Œåœ¨ä¹¦ä¸Šè®°ä¸‹äº†ä¸€ä¸ªç®€å•æ˜“æ‡‚çš„æƒ³æ³•ï¼Œä¹‹ååœ¨[è¯¾ç¨‹è®¾è®¡ 1](#toc-heading-12)é‡æ–°è¿›è¡Œäº†ç¼–å†™ã€‚   
    ç¼–å†™è¿™ä¸ªç¨‹åºé‡åˆ°ä¸€ä¸ªæ–°æ‰‹å®¹æ˜“å¿˜è®°çš„é—®é¢˜ï¼š  
    - mov æŒ‡ä»¤ä¸èƒ½åœ¨ä¸¤ä¸ªå­˜å‚¨å•å…ƒä¹‹é—´ç›´æ¥ä¼ é€æ•°æ®  

    ä»¥ä¸‹æ˜¯æ—§ç‰ˆæœ¬çš„ä»£ç ï¼š
    ```x86asm
    assume cs:codeseg

    data segment
    db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
    db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
    db '1993','1994','1995'

    dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
    dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000

    dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
    dw 11542,14430,15257,17800
    data ends

    table segment
    db 21 dup ('year summ ne ?? ')
    table ends

    stack segment
    db 16 dup (0)
    stack ends

    codeseg segment
    start:
        mov ax,data
        mov ds,ax

        mov ax,stack
        mov ss,ax
        mov sp,10H

        mov ax,table
        mov es,ax

        mov bx,0
        mov bp,0
        mov di,0A8H
        mov cx,15H
    s:
        push cx

        mov cx,4
        mov si,0
        i:
        mov dl,ds:[bp+si]
        mov es:[bx+si],dl
        inc si
        loop i

        push bp
        add bp,54H
        mov dx,ds:[bp]
        mov es:[bx+5],dx
        mov dx,ds:[bp+2]
        mov es:[bx+7],dx
        pop bp

        mov dx,ds:[di]
        mov es:[bx+0AH],dx
        
        mov DX,es:[bx+7]
        mov AX,es:[bx+5]
        div word ptr es:[bx+0AH]
        mov es:[bx+0DH],AX

        add bx,10H
        add bp,4H
        add di,2H
        pop cx
    loop s

        mov ax,4c00H
        int 21H
    codeseg ends

    end start
    ```

## å®éªŒ 8 åˆ†æä¸€ä¸ªå¥‡æ€ªçš„ç¨‹åº

- åˆ†æä¸‹é¢çš„ç¨‹åºï¼Œåœ¨è¿è¡Œå‰æ€è€ƒï¼Œè¿™ä¸ªç¨‹åºå¯ä»¥æ­£ç¡®è¿”å›å—ï¼Ÿ  
    è¿è¡Œåå†æ€è€ƒï¼šä¸ºä»€ä¹ˆæ˜¯è¿™ç§ç»“æœï¼Ÿ  
    é€šè¿‡è¿™ä¸ªç¨‹åºåŠ æ·±å¯¹ç›¸å…³å†…å®¹çš„ç†è§£ã€‚
    ```x86asm
    assume cs:codeseg

    codeseg segment

        mov ax,4c00h    ;0A77:0000
        int 21h

    start:    
        mov ax,0
    s:  nop             ;0A77:0008
        nop             ;0A77:0009

        mov di,offset s ;0A77:000A ;mov di,0008
        mov si,offset s2           ;mov si,0020
        mov ax,cs:[si]             ;mov ax,F6EB
        mov cs:[di],ax             ;0A77:0008 EB
                                   ;0A77:0009 F6
    s0: jmp short s     ;0A77:0016 EBF0

    s1: mov ax,0        ;0A77:0018
        int 21h
        mov ax,0
                        ;æ³¨æ„è¿™é‡Œåœ¨ç¼–è¯‘çš„æ—¶å€™å·²ç»è®¡ç®—å¥½äº†offset
    s2: jmp short s1    ;0A77:0020 EBF6 ;F6h = -10 
        nop             ;0A77:0022 NOP

    codeseg ends

    end start
    ```
    å¯ä»¥æ­£ç¡®è¿”å›  
    - è¿™é‡Œæ ¹æ®åšä¸»æœºå™¨ä¸Šè·‘çš„æ•°æ®åšäº†æ³¨é‡Š  
    - ç¨‹åºçš„å…¥å£ä¸º mov ax, 0  
    - æ³¨æ„åˆ°æŒ‡ä»¤jmp short s1å 2å­—èŠ‚  
    äºæ˜¯æŒ‡ä»¤
        ```
        mov di,offset s
        mov si,offset s2
        mov ax,cs:[si]
        mov cs:[di],ax
        ```
        ç›¸å½“äºä½¿ç”¨é€šç”¨å¯„å­˜å™¨AXåšä¸­è½¬å°†s2å¤„çš„æŒ‡ä»¤å¤åˆ¶åˆ°så¤„  
    - æœ€åå†è·³è½¬è‡³så¤„æ‰§è¡Œå¤åˆ¶è¿‡æ¥çš„æŒ‡ä»¤  
        æ³¨æ„jmp short s1æ˜¯æ®µå†…è½¬ç§»çš„çŸ­è½¬ç§»ï¼Œå…¶åªä¿®æ”¹IPå¯„å­˜å™¨    
        è€Œç¼–è¯‘çš„æ—¶å€™å·²ç»è®¡ç®—å¥½äº†offsetï¼šè¡¥ç è¡¨ç¤ºçš„8bitçš„æœ‰ç¬¦å·æ•° F6hï¼Œå³-10  
        é€šè¿‡Debugå¯çŸ¥sçš„æ®µå†…åç§»åœ°å€ä¸º 8  
        è¯»å–s2å¤„å¤åˆ¶è¿‡æ¥çš„æŒ‡ä»¤åï¼ŒIPæŒ‡å‘10ï¼Œ
        æ‰€ä»¥åœ¨æ‰§è¡Œè¯¥æŒ‡ä»¤å,IPå°†æŒ‡å‘0  
        å› æ­¤å¯ä»¥æ­£ç¡®è¿”å›  

## å®éªŒ 9 æ ¹æ®ææ–™ç¼–ç¨‹
- ç¼–ç¨‹ï¼šåœ¨å±å¹•ä¸­é—´åˆ†åˆ«æ˜¾ç¤ºç»¿è‰²ã€ç»¿åº•çº¢è‰²ã€ç™½åº•è“è‰²çš„å­—ç¬¦ä¸²'welcome to masm!'ã€‚  
    ç¼–ç¨‹æ‰€éœ€çš„åªæ˜¯é€šè¿‡é˜…è¯»ã€åˆ†æä¹¦æœ¬ä¸Šçš„ææ–™è·å¾—ã€‚
    - æ˜¾ç¤ºç¼“å†²åŒºB8000h-BFFFFhå…±32KBçš„ç©ºé—´
    - æˆ‘ä»¬å°†å­—ç¬¦å†™å…¥ç¬¬0é¡µçš„æ˜¾ç¤ºç¼“å†²åŒºä¸­  
        å³B8000h-B8F9Fhå…±FA0h = 4000ä¸ªå­—èŠ‚
    - 1ä¸ªå­—ç¬¦åœ¨æ˜¾ç¤ºç¼“å†²åŒºä¸­å ç”¨2ä¸ªå­—èŠ‚ï¼Œä½å­—èŠ‚å­˜å‚¨ASCIIç ï¼Œé«˜å­—èŠ‚å­˜å‚¨å­—ç¬¦çš„å±æ€§  
        å¯ä»¥çœ‹å‡ºï¼Œåœ¨æ˜¾ç¤ºç¼“å†²åŒºä¸­ï¼Œå¶æ•°åœ°å€å­˜æ”¾å­—ç¬¦ï¼Œå¥‡æ•°åœ°å€å­˜æ”¾å­—ç¬¦å±æ€§
    - æ ¹æ®å­—ç¬¦å±æ€§çš„æ ¼å¼ï¼Œæˆ‘ä»¬å¯ä»¥ç»™å‡ºä¸‰ä¸ªå­—ç¬¦ä¸²æ‰€éœ€è¦å†™å…¥çš„å±æ€§ï¼š  
        ç»¿è‰²:02hã€ç»¿åº•çº¢è‰²:24hã€ç™½åº•è“è‰²:71h
    - ç¼–å†™è¿™ä¸ªç¨‹åºé‡åˆ°ä¸€äº›æ–°æ‰‹å®¹æ˜“å¿˜è®°çš„é—®é¢˜ï¼š
        - 16è¿›åˆ¶æ•°å­—å­—æ¯å¼€å¤´æ—¶ï¼Œå¿˜è®°åŠ å‰å¯¼0ã€‚  
            æŠ¥error A2006 undefined symbol  
            ä¾‹å¦‚0B800hé”™å†™ä¸ºB800h
        - ä¸å¯ä»¥ä»¥'idata:[]'æ–¹å¼å¯»å€
        - åœ¨ä½¿ç”¨BPè¿›è¡Œå¯„å­˜å™¨é—´æ¥å¯»å€æ—¶ï¼Œç¼ºçœçš„æ®µåœ°å€æ˜¯SS  

    ä¸‹é¢æ˜¯ä»£ç ï¼š
    ```x86asm
    assume cs:code

    data segment
    db 'welcome to masm!'
    db 02H,24H,71H
    data ends

    stack segment
    db 10H dup (0)
    stack ends

    code segment
    start:
        mov ax,data
        mov ds,ax 
        
        mov ax,0b878H
        mov es,ax

        mov ax,stack
        mov ss,ax
        mov sp,10H

        mov cx,3
        mov bx,10H
        mov bp,0
    row:
        push cx
        mov cx,10H
        mov di,0
        mov si,0
        mov al,[bx]
    column:
        mov dl,[si]
        mov es:[bp+di+40H],dl
        inc di
        mov es:[bp+di+40H],al
        inc si
        inc di
    loop column
        pop cx
        inc bx
        add bp,0a0H
    loop row

        mov ax,4c00H
        int 21H
    code ends

    end start
    ```
    ![LAB9 Result](http://bucket.jiabi.tech/images/asmLab/lab9_result.jpg)

## å®éªŒ 10 ç¼–å†™å­ç¨‹åº

- è¿™ä¸‰ä¸ªå­ç¨‹åºå°†åé¢çš„è¯¾ç¨‹è®¾è®¡ 1çš„éš¾åº¦æ›²çº¿å˜å¾—æ›´å¹³æ»‘  
    åšä¸»åˆšå¼€å§‹å› ä¸ºå­ç¨‹åºç¼–å†™çš„é²æ£’æ€§ä¸å¤Ÿé«˜ï¼Œæ²¡æœ‰è¿›è¡Œæ›´å¤šçš„æµ‹è¯•ä¾¿æ•´åˆè¿›äº†è¯¾ç¨‹è®¾è®¡ 1ï¼Œå¯¼è‡´åé¢èŠ±è´¹äº†å¤§é‡çš„æ—¶é—´è°ƒè¯•ï¼Œè¯·å¼•ä»¥ä¸ºé‰´ï¼
1. æ˜¾ç¤ºå­—ç¬¦ä¸²
    ```x86asm
    assume cs:code

    data segment
        db 'Welcome to masm!',0
    data ends

    stack segment
        dw 10h dup (0)
    stack ends

    code segment
    start:
        mov ax,stack
        mov ss,ax
        mov sp,10h

        mov ax,data
        mov ds,ax

        mov dh,8
        mov dl,3
        mov cl,2
        mov si,0
        call show_str

        mov ax,4c00h
        int 21h

    show_str:
        push ax
        push bx
        push cx
        push dx
        push di
        push si

        mov ax,0B800H
        mov es,ax

        mov al,2
        mul dl
        mov di,ax
        mov al,0a0h
        mul dh
        mov bx,ax
        add bx,di
        
        mov di,0
        mov al,cl

    str_loop:
        mov cx,0
        mov cl,[si]
        jcxz ok

        mov ch,al
        mov es:[bx+di],cx
        
        inc si
        add di,2

        jmp short str_loop
    ok:
        pop si
        pop di
        pop dx
        pop cx
        pop bx
        pop ax
    ret

    code ends

    end start
    ```
2. è§£å†³é™¤æ³•æº¢å‡ºçš„é—®é¢˜
    ```x86asm
    assume cs:code 

    stack segment
        dw 10h dup (0)
    stack ends

    code segment

    start:
        mov ax,4240h
        mov dx,000fh
        mov cx,000ah
        call divdw

        mov ax,4c00h
        int 21h
    
    ;å› ä¸ºæ€•DOSæ— æ³•æ˜¾ç¤ºä¸­æ–‡ï¼Œæ‰€ä»¥ç”¨è¹©è„šçš„è‹±æ–‡å†™äº†æ³¨é‡Š
    ;dword size div without overflow
    ;parameter:
    ;   Dividend = dx<<16 + ax
    ;   register ax ==> the low  2Bytes of the Dividend
    ;   register dx ==> the high 2Bytes of the Dividend
    ;   register cx ==> Divisor
    ;return:
    ;   Quotient = dx<<16 + ax
    ;   register ax ==> the low  2Bytes of the Quotient
    ;   register dx ==> the high 2Bytes of the Quotient
    ;   register cx ==> Remainder
    divdw:
        push bx

        push ax

        mov ax,dx
        mov dx,0
        div cx

        mov bx,ax
        
        pop ax
        div cx
        mov cx,dx
        mov dx,bx

        pop bx
    ret

    code ends

    end start

    ```
3. æ•°å€¼æ˜¾ç¤º
    ```x86asm
    assume cs:code

    data segment
        dw 10h dup (0)
    data ends

    stack segment
        dw 10h dup (0)
    stack ends

    code segment

    start:
        mov bx,data
        mov ds,bx
        mov si,0

        mov bx,stack
        mov ss,bx
        mov sp,10h

        mov ax,12666
        call dtoc   

        mov dh,8
        mov dl,3
        mov cl,2
        call show_str

        mov ax,4c00h
        int 21h

    ;å› ä¸ºæ€•DOSæ— æ³•æ˜¾ç¤ºä¸­æ–‡ï¼Œæ‰€ä»¥ç”¨è¹©è„šçš„è‹±æ–‡å†™äº†æ³¨é‡Š
    ;digit to char word version
    ;trans word size(2Bytes) digit to C char* style String
    ;parameter:
    ;   register ax ==> digit
    ;   ds:si       ==> the address of the String
    ;return:
    ;   void
    dtoc:
        push bx
        push cx
        push dx
        push di
        push si

        mov bx,10
        mov di,0
    loopDiv:
        mov dx,0
        div bx
        inc di
        add dx,30h
        push dx

        mov cx,ax
        inc cx
    loop loopDiv

        mov cx,di
    fillChar:
        pop dx
        mov ds:[si],dl
        inc si
    loop fillChar

        mov byte ptr ds:[si],0

        pop si
        pop di
        pop dx
        pop cx
        pop bx
    ret

    show_str:
        push ax
        push bx
        push cx
        push dx
        push di
        push si

        mov ax,0B800H
        mov es,ax

        mov al,2
        mul dl
        mov di,ax
        mov al,0a0h
        mul dh
        mov bx,ax
        add bx,di
        
        mov di,0
        mov al,cl

    str_loop:
        mov cx,0
        mov cl,[si]
        jcxz ok

        mov ch,al
        mov es:[bx+di],cx
        
        inc si
        add di,2

        jmp short str_loop
    ok:
        pop si
        pop di
        pop dx
        pop cx
        pop bx
        pop ax
    ret

    code ends

    end start
    ```

## è¯¾ç¨‹è®¾è®¡ 1

- ä»»åŠ¡ï¼šå°†[å®éªŒ 7](#toc-heading-8)ä¸­çš„Power ideaå…¬å¸çš„æ•°æ®åœ¨å±å¹•ä¸Šæ˜¾ç¤ºå‡ºæ¥ã€‚  
    æ³¨æ„ï¼Œå› ä¸ºç¨‹åºè¦æ˜¾ç¤ºçš„æ•°æ®æœ‰äº›ä»¥åŠå¤§äº 2^16-1 = 65535,åº”è¯¥ç¼–å†™ä¸€ä¸ªæ–°çš„æ•°æ®åˆ°å­—ç¬¦ä¸²è½¬åŒ–çš„å­ç¨‹åºï¼Œå®Œæˆdowrdç±»å‹æ•°æ®åˆ°å­—ç¬¦ä¸²çš„è½¬åŒ–ã€‚ 
- è¿™é‡Œå¯¹[å®éªŒ 7](#toc-heading-8)çš„ç›¸å…³ä»£ç è¿›è¡Œäº†é‡æ„
- ä»£ç ä¸­åšä¸»é‡‡ç”¨äº†ä¸¤ä¸ªä¸åŒçš„å­ç¨‹åºdtocã€dtocDwæ¥å®Œæˆä¸åŒçš„æ•°æ®ç±»å‹çš„è½¬æ¢ã€‚  
    **å®é™…ä¸Šä¸ç”¨è¿™ä¹ˆéº»çƒ¦ï¼š**  
    å®Œå…¨å¯ä»¥é‡‡ç”¨åœ¨dataæ®µé¢„ç•™è¶³å¤Ÿçš„ç©ºæ ¼ä¸²çš„åŒºåŸŸï¼Œè€Œdtocåªéœ€è¦å¡«å†™å¯¹åº”çš„åè¿›åˆ¶æ•°å­—çš„ASCIIå³å¯ï¼Œä¸éœ€è¦ä¸»åŠ¨å¡«å……ç©ºç™½å­—ç¬¦(fillSpaceã€fillSpaceDw)ã€‚  
    è¿™æ ·åªéœ€è¦ç¼–å†™ä¸€ä¸ªdtocå³å¯ï¼Œä¹Ÿä¸éœ€è¦é¢å¤–çš„å‚æ•°ï¼Œå®é™…æ˜¾ç¤ºçš„æ•ˆæœä¹Ÿä¼šæ›´åŠ ç¾è§‚ã€‚  
    æ³¨ï¼šé¢„ç•™è¶³å¤Ÿçš„æ•°æ®ç©ºé—´ç”±è°ƒç”¨è€…ä¿è¯
- ç¼–å†™è¿™ä¸ªç¨‹åºé‡åˆ°çš„ä¸€äº›é—®é¢˜ï¼š  
    - CXä¸º0æ—¶ï¼Œç›´æ¥è¿›å…¥å¾ªç¯æ‰§è¡ŒloopæŒ‡ä»¤ï¼ŒCXä¼šæº¢å‡ºè‡³FFFFh  
        dtocä¸­çš„jcxz overFillä¾¿æ˜¯ä¸ºäº†è§£å†³è¿™ä¸ªé—®é¢˜
    - å­ç¨‹åºè®¾è®¡æ—¶æ³¨æ„ä¿å­˜ç°åœºï¼Œå³åˆ©ç”¨æ ˆä¿æŠ¤å„ä¸ªä¼šç ´ååŸæœ‰å€¼çš„å¯„å­˜å™¨  
        è¿™é‡Œé‡åˆ°äº†è°ƒç”¨show_stræ— æ³•æ˜¾ç¤ºå¤šè¡Œçš„é—®é¢˜
    - æ³¨æ„æ ˆæ“ä½œè¶Šç•Œçš„é—®é¢˜ï¼Œç”±äº8086æ±‡ç¼–ä¸­ç¡®ä¿æ ˆæ“ä½œåˆæ³•çš„ä»»åŠ¡äº¤ç»™äº†ç¨‹åºè®¾è®¡è€…ï¼Œå› æ­¤æ ˆæ“ä½œè¶Šç•Œå¯èƒ½å¼•èµ·ä¸€ç³»åˆ—ä¸¥é‡çš„é—®é¢˜ã€‚  
        è¿™é‡Œé‡åˆ°äº†SPåˆå§‹å€¼è®¾ç½®é”™è¯¯å¼•èµ·çš„æ ˆæ“ä½œè¶Šç•Œè¿›è€Œä¿®æ”¹codeæ®µæ•°æ®çš„é—®é¢˜  

```x86asm
assume cs:code

source segment
db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
db '1993','1994','1995'

dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000

dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
dw 11542,14430,15257,17800
source ends

table segment
    db 21 dup ('year summ ne ?? ')
            ;0123456789012345
table ends

data segment
    db 21 dup ('year 0123456789 01234 01234 ')
            ;0123456789012345678901234567
data ends

stack segment
    dw 20h dup (0)
stack ends

code segment

start:
    mov ax,source
    mov es,ax

    mov ax,table
    mov ds,ax

    mov ax,stack
    mov ss,ax
    mov sp,40h

    call trans

    mov ax,table
    mov es,ax

    mov ax,data
    mov ds,ax

;trans table to C char* style String
    mov cx,21
    mov bx,0
    mov bp,0
toStr:
    ;copy year field from table to data
    ;storage in [bx].0--[bx].3
    ;data source es:[bp].0--es:[bp].3
    mov dx,es:[bp].0
    mov [bx].0,dx
    mov dx,es:[bp].2
    mov [bx].2,dx
    ;insert '|' between year filed and income field
    mov byte ptr [bx].4,124
    ;call dtoc dword version
    ;storage in [bx].5--[bx].14
    ;data source es:[bp].5--es:[bp].8
    add si,5
    mov ax,es:[bp].5
    mov dx,es:[bp].7
    call dtocDw
    ;insert '|' between income field and employee field
    mov byte ptr [bx].15,124
    ;call dtoc word version
    ;storage in [bx].16--[bx].20
    ;data source es:[bp].10--es:[bp].11
    add si,11
    mov ax,es:[bp].10
    call dtoc
    ;insert '|' between employee field and avenger field
    mov byte ptr [bx].21,124
    ;call dtoc word version
    ;storage in [bx].22--[bx].26
    ;data source es:[bp].13--es:[bp].14
    add si,6
    mov ax,es:[bp].13
    call dtoc
    ;insert '\0' at the end
    mov byte ptr [bx].27,0

    add si,6
    add bx,28
    add bp,16
loop toStr

;show data segment
    mov cx,21
    mov si,0
    mov dh,2
    mov dl,26
showData:
    push cx
    mov cl,2
    call show_str
    add si,28
    inc dh
    pop cx
loop showData

    mov ax,4c00H
    int 21H

;trans source to table
;storage in table segent
trans:
    push ax
    push bx
    push cx
    push dx
    push bp
    push si

    mov bp,0

    mov cx,21
    mov bx,0
year:
    push cx

        mov cx,4
        mov si,0
    yLoop:
        mov dl,es:[bp+si]
        mov [bx].0[si],dl
        inc si
    loop yLoop

    add bp,4
    add bx,10h
    pop cx
loop year

    mov cx,21
    mov bx,0
income:
    push cx

        mov cx,2
        mov si,0
    iLoop:
        mov dx,es:[bp+si]
        mov [bx].5[si],dx
        add si,2
    loop iLoop

    add bp,4
    add bx,10h
    pop cx
loop income

    mov cx,21
    mov bx,0
employee:
    push cx
    
    mov si,0
    mov dx,es:[bp+si]
    mov [bx].10[si],dx

    add bp,2
    add bx,10h
    pop cx
loop employee

    mov cx,21
    mov bx,0
calc:
    push cx
    
    mov ax,[bx].5
    mov dx,[bx].7
    mov bp,[bx].10
    div bp
    mov [bx].13,ax

    add bx,10h
    pop cx
loop calc

    pop si
    pop bp
    pop dx
    pop cx
    pop bx
    pop ax
ret

;digit to char word version
;trans word size(2Bytes) digit to C char* style String
;parameter:
;   register ax ==> digit
;   ds:si       ==> the address of the String
;return:
;   void
dtoc:
    push bx
    push cx
    push dx
    push di
    push si

    mov bx,10
    mov di,0
loopDiv:
    mov dx,0
    div bx
    inc di
    add dx,30h
    push dx

    mov cx,ax
    inc cx
loop loopDiv

    mov cx,di
fillChar:
    pop dx
    mov ds:[si],dl
    inc si
loop fillChar

    mov cx,5
    sub cx,di
    jcxz overFill
fillSpace:
    mov byte ptr ds:[si],32
    inc si
loop fillSpace

overFill:
    mov byte ptr ds:[si],0

    pop si
    pop di
    pop dx
    pop cx
    pop bx
ret

;digit to char dword version
;trans dword size(4Bytes) digit to C char* style String
;parameter:
;   register ax ==> the low  2Bytes of the digit
;   register dx ==> the high 2Bytes of the digit
;   ds:si       ==> the address of the String
;return:
;   void
dtocDw:
    push cx
    push dx
    push di
    push si

    mov di,0
loopDivDw:
    mov cx,10
    call divdw
    inc di
    add cx,30h
    push cx

    mov cx,ax
    inc cx
loop loopDivDw

    mov cx,di
fillCharDw:
    pop dx
    mov ds:[si],dl
    inc si
loop fillCharDw

    mov cx,10
    sub cx,di
    jcxz overFillDw
fillSpaceDw:
    mov byte ptr ds:[si],32
    inc si
loop fillSpaceDw

overFillDw:
    mov byte ptr ds:[si],0

    pop si
    pop di
    pop dx
    pop cx
ret

;dword size div without overflow
;parameter:
;   Dividend = dx<<16 + ax
;   register ax ==> the low  2Bytes of the Dividend
;   register dx ==> the high 2Bytes of the Dividend
;   register cx ==> Divisor
;return:
;   Quotient = dx<<16 + ax
;   register ax ==> the low  2Bytes of the Quotient
;   register dx ==> the high 2Bytes of the Quotient
;   register cx ==> Remainder
divdw:
    push bx

    push ax

    mov ax,dx
    mov dx,0
    div cx

    mov bx,ax
    
    pop ax
    div cx
    mov cx,dx
    mov dx,bx

    pop bx
ret

show_str:
    push ax
    push bx
    push cx
    push dx
    push di
    push si

    mov ax,0B800H
    mov es,ax

    mov al,2
    mul dl
    mov di,ax
    mov al,0a0h
    mul dh
    mov bx,ax
    add bx,di
    
    mov di,0
    mov al,cl

str_loop:
    mov cx,0
    mov cl,[si]
    jcxz ok

    mov ch,al
    mov es:[bx+di],cx
    
    inc si
    add di,2

    jmp short str_loop
ok:
    pop si
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
ret

code ends

end start
```
![Course Design 1](http://bucket.jiabi.tech/images/asmLab/CourseDesign1.jpg)

## å®éªŒ 11 ç¼–å†™å­ç¨‹åº

- ç¼–å†™ä¸€ä¸ªå­ç¨‹åºï¼Œå°†åŒ…å«ä»»æ„å­—ç¬¦ï¼Œä»¥ 0 ç»“å°¾çš„å­—ç¬¦ä¸²ä¸­çš„å°å†™å­—æ¯è½¬å˜æˆå¤§å†™å­—æ¯ï¼Œæè¿°å¦‚ä¸‹ã€‚  
    åç§°ï¼šletterc  
    åŠŸèƒ½ï¼šå°†ä»¥ 0 ç»“å°¾çš„å­—ç¬¦ä¸²ä¸­çš„å°å†™å­—æ¯è½¬å˜æˆå¤§å†™å­—æ¯  
    å‚æ•°ï¼šds:siæŒ‡å‘å­—ç¬¦ä¸²é¦–åœ°å€  

```x86asm
assume cs:code

data segment
    db "Beginner's All-purpose Symbolic Instrcution Code.",0
data ends

stack segment
    dw 10h dup (0)
stack ends

code segment

begin:
    mov ax,data
    mov ds,ax

    mov ax,stack
    mov ss,ax
    mov sp,10h

    mov si,0
    call letterc

    mov dh,8
    mov dl,3
    mov cl,2
    mov si,0
    call show_str

    mov ax,4c00h
    int 21h

letterc:
    push si
    push cx
    pushf

loopLetter:
    mov ch,0
    mov cl,[si]
    cmp cl,61h
    jb nextLetter
    cmp cl,7ah
    ja nextLetter
    and cl,0dfh
    mov [si],cl

nextLetter: 
    inc si
    inc cx
loop loopLetter

    popf
    pop cx
    pop si
ret

show_str:
    push ax
    push bx
    push cx
    push dx
    push di
    push si

    mov ax,0B800H
    mov es,ax

    mov al,2
    mul dl
    mov di,ax
    mov al,0a0h
    mul dh
    mov bx,ax
    add bx,di
    
    mov di,0
    mov al,cl

str_loop:
    mov cx,0
    mov cl,[si]
    jcxz ok

    mov ch,al
    mov es:[bx+di],cx
    
    inc si
    add di,2

    jmp short str_loop
ok:
    pop si
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
ret

code ends

end begin
```

## å®éªŒ 12 ç¼–å†™0å·ä¸­æ–­çš„å¤„ç†ç¨‹åº

- ç¼–å†™0å·ä¸­æ–­çš„å¤„ç†ç¨‹åºï¼Œä½¿å¾—é™¤æ³•æº¢å‡ºå‘ç”Ÿæ—¶ï¼Œåœ¨å±å¹•ä¸­é—´æ˜¾ç¤ºå­—ç¬¦ä¸²"divide error!"ï¼Œç„¶åè¿”å›åˆ°DOSã€‚
- è¿™é‡Œæ²¡æœ‰å¯¹ä¸­æ–­å¤„ç†ç¨‹åºä½¿ç”¨åˆ°çš„å¯„å­˜å™¨è¿›è¡Œä¿æŠ¤

```x86asm
assume cs:code

code segment

start:
    mov ax,cs
    mov ds,ax
    mov si,do0

    mov ax,0
    mov es,ax
    mov di,200h

    mov cx,do0end-do0
    cld
    rep movsb

    mov word ptr es:[4*0],200h
    mov word ptr es:[4*0+2],0

    mov ax,4c00h
    int 21h

do0:
    jmp do0start
    db "divide error!"
do0start:
    mov ax,cs
    mov ds,ax
    mov si,202h

    mov ax,0b800h
    mov es,ax
    mov di,12*160+34*2

    mov cx,13
    S:
    mov al,[si]
    mov es:[di],al
    inc di
    mov byte ptr es:[di],2
    inc di
    inc si    
    loop s

    mov ax,4c00h
    int 21h
do0end:
    nop

code ends

end start
```

## å®éªŒ 13 ç¼–å†™ã€åº”ç”¨ä¸­æ–­ä¾‹ç¨‹

1. ç¼–å†™å¹¶å®‰è£…int 7chä¸­æ–­ä¾‹ç¨‹ï¼ŒåŠŸèƒ½ä¸ºæ˜¾ç¤ºä¸€ä¸ªç”¨0ç»“æŸçš„å­—ç¬¦ä¸²ï¼Œä¸­æ–­ä¾‹ç¨‹å®‰è£…åœ¨0:200å¤„ã€‚  
    å‚æ•°ï¼š(dh)=è¡Œå· (dl)=åˆ—å· (cl)=é¢œè‰² ds:siæŒ‡å‘å­—ç¬¦ä¸²é¦–åœ°å€

- ä¸­æ–­ä¾‹ç¨‹

```x86asm
assume cs:code

code segment

start:
    mov ax,cs
    mov ds,ax
    mov si,do7c

    mov ax,0
    mov es,ax
    mov di,200h

    mov cx,do7cend-do7c
    cld
    rep movsb

    mov word ptr es:[4*7ch],200h
    mov word ptr es:[4*7ch+2],0

    mov ax,4c00h
    int 21h 

do7c:
    push ax
    push cx
    push si
    push di
    push es    

    mov ax,0b800h
    mov es,ax
    mov di,0
    mov al,160
    mul dh
    add di,ax
    mov al,2
    mul dl
    add di,ax

    mov al,cl
s: 
    mov cx,0
    mov cl,ds:[si]
    jcxz ok
    mov ch,al
    mov es:[di],cx

    inc si
    add di,2

    jmp s
ok:
    pop es
    pop di
    pop si
    pop cx
    pop ax

    iret
do7cend:
    nop

code ends
end start
```

- æµ‹è¯•ä»£ç 

```x86asm
;TEST LAB13_1
assume cs:code

data segment
    db "Welcome to masm!",0
data ends

stack segment
    dw 10h dup (0)
stack ends

code segment

start:
    mov ax,data
    mov ds,ax

    mov ax,stack
    mov ss,ax
    mov sp,10h

    mov dh,10
    mov dl,10
    mov cl,2
    mov si,0
    int 7ch
    
    mov ax,4c00h
    int 21h

code ends
end start
```

2. ç¼–å†™å¹¶å®‰è£…int 7chä¸­æ–­ä¾‹ç¨‹ï¼ŒåŠŸèƒ½ä¸ºå®ŒæˆloopæŒ‡ä»¤çš„åŠŸèƒ½ã€‚  
    å‚æ•°ï¼š(cx)=å¾ªç¯æ¬¡æ•° (bx)=ä½ç§»

- ä¸­æ–­ä¾‹ç¨‹

```x86asm
assume cs:code

code segment

start:
    mov ax,cs
    mov ds,ax
    mov si,do7c

    mov ax,0
    mov es,ax
    mov di,200h

    mov cx,do7cend-do7c
    cld
    rep movsb

    mov word ptr es:[4*7ch],200h
    mov word ptr es:[4*7ch+2],0

    mov ax,4c00h
    int 21h 

do7c:
    dec cx
    jcxz ok
    push bp
    mov bp,sp
    add [bp+2],bx
    pop bp
ok:
    iret
do7cend:
    nop

code ends
end start
```

- æµ‹è¯•ä»£ç 

```x86asm
;TEST LAB13_2
assume cs:code

stack segment
    dw 10h dup (0)
stack ends

code segment

start:
    mov ax,stack
    mov ss,ax
    mov sp,10h

    mov ax,0b800h
    mov es,ax
    mov di,160*12
    mov bx,s-sEnd
    mov cx,80
s:
    mov byte ptr es:[di],'!'
    inc di
    mov byte ptr es:[di],4
    inc di
    int 7ch
sEnd:
    nop

    mov ax,4c00h
    int 21h

code ends
end start
```

3. ä¸‹é¢çš„ç¨‹åºï¼Œåˆ†åˆ«åœ¨å±å¹•çš„ç¬¬2ã€4ã€6ã€8è¡Œæ˜¾ç¤º4å¥è‹±æ–‡è¯—ï¼Œè¡¥å…¨ç¨‹åºã€‚

```x86asm
assume cs:code

code segment

    s1: db 'Good,better,best,','$'
    s2: db 'Never let it rest,','$'
    s3: db 'Till good is better,','$'
    s4: db 'And better,best.','$'
    s : dw offset s1,offset s2,offset s3,offset s4
    row: db 2,4,6,8
start:
    mov ax,cs
    mov ds,ax
    mov bx,offset s
    mov si,offset row
    mov cx,4
ok:
    mov bh,0
    mov dh,ds:[si]
    mov dl,0
    mov ah,2
    int 10h

    mov dx,ds:[bx]
    mov ah,9
    int 21h
    inc si
    add bx,2
    loop ok

    mov ax,4c00h
    int 21h

code ends

end start
```

## å®éªŒ 14 è®¿é—®CMOS RAM

- ç¼–ç¨‹ï¼Œä»¥"å¹´/æœˆ/æ—¥ æ—¶:åˆ†:ç§’"çš„æ ¼å¼ï¼Œæ˜¾ç¤ºå½“å‰çš„æ—¥æœŸã€æ—¶é—´ã€‚
    - ä¸å¾—ä¸è¯´ï¼Œè¿™æœ¬ä¹¦çš„æ›²çº¿æ˜¯ç›¸å½“é¡ºæ»‘çš„  
    åœ¨å®éªŒ13.3ä¸­ï¼Œç‹çˆ½è€å¸ˆæŠŠå®éªŒ14å¯èƒ½è¦ç”¨åˆ°çš„æŠ€å·§ä»¥å®éªŒçš„å½¢å¼å‘ˆç°
    - åšä¸»å‡ºäº†ä¸ªæŒºæç¬‘çš„Bugï¼Œå› ä¸ºå¿˜è®°åŠ ä¸Š"inc si"  
    å¯¼è‡´æ˜¾ç¤ºçš„æ—¶é—´æ˜¯"20/20/20 20:20:20",è¿˜è§‰å¾—å¥½å·§ :joy:
    - æŸ¥éªŒç»“æœæ˜¯å¦æ­£ç¡®å¯ä»¥ç”¨dateä¸timeå‘½ä»¤

    ```x86asm
    assume cs:code

    stack segment
        dw 10h dup (0)
    stack ends

    code segment
        
        dataPos: db 9,8,7,4,2,0
    start:
        mov ax,stack
        mov ss,ax
        mov sp,10h

        mov ax,cs
        mov ds,ax
        mov si,offset dataPos

        mov bx,0b800h
        mov es,bx
        mov di,160*12+32*2

        mov cx,6
    nextField:
        push cx
        
        mov al,ds:[si]
        inc si
        out 70h,al
        in al,71h

        mov ah,al
        mov cl,4
        shr ah,cl
        and al,0fh
        
        add ah,30h
        add al,30h

        mov es:[di],ah
        inc di
        mov byte ptr es:[di],2
        inc di
        mov es:[di],al
        inc di
        mov byte ptr es:[di],2
        inc di

        pop cx
        
        cmp cx,1
        je doNotShow
        
        cmp cx,4
        mov ah,32   ;ASCII 32 (space)
        je show
        mov ah,47   ;ASCII 47 /
        ja show
        mov ah,58   ;ASCII 58 :
        jb show
    show:
        mov es:[di],ah
        inc di
        mov byte ptr es:[di],2
        inc di

    doNotShow:
        loop nextField

        mov ax,4c00h
        int 21h

    code ends

    end start
    ```

## å®éªŒ 15 å®‰è£…æ–°çš„int9ä¸­æ–­ä¾‹ç¨‹

- å®‰è£…ä¸€ä¸ªæ–°çš„int 9ä¸­æ–­ä¾‹ç¨‹  
    åŠŸèƒ½ï¼šåœ¨DOSä¸‹ï¼ŒæŒ‰ä¸‹'A'é”®åï¼Œé™¤éä¸å†æ¾å¼€ï¼Œå¦‚æœæ¾å¼€ï¼Œå°±æ˜¾ç¤ºæ»¡å±å¹•çš„'A'ï¼Œå…¶ä»–é”®ç…§å¸¸å¤„ç†ã€‚
- è¿™é‡Œæ²¡æœ‰å†™æ¢å¤ä¸­æ–­å‘é‡çš„ç¨‹åºï¼Œæ„Ÿè§‰ç”¨äº†è¿™ä¸ªä¼šè¢«é€¼ç–¯æ‰ 233

```x86asm
assume cs:code

stack segment
    dw 20h dup (0)
stack ends

code segment

start:
    mov ax,stack
    mov ss,ax
    mov sp,20h

    push cs
    pop ds

    mov ax,0
    mov es,ax
    mov di,200h

    push es:[4*9]
    pop es:[di]
    add di,2
    push es:[4*9+2]
    pop es:[di]
    add di,2

    mov si,newInt9
    mov cx,newInt9End-newInt9
    cld
    rep movsb

    cli
    mov word ptr es:[4*9],204h
    mov word ptr es:[4*9+2],0
    sti

    mov ax,4c00h
    int 21h

newInt9:
    push ax
    push bx
    push cx
    push es

    in al,60h

    pushf
    call dword ptr cs:[200h]

    cmp al,10011110b
    jne notA

    mov ax,0b800h
    mov es,ax
    mov bx,0
    mov cx,2000
    s:
        mov byte ptr es:[bx],41h
        inc bx
        mov byte ptr es:[bx],2
        inc bx
    loop s

notA:
    pop es
    pop cx
    pop bx
    pop ax
    iret
newInt9End:
    nop;

code ends

end start
```

## å®éªŒ 16 ç¼–å†™åŒ…å«å¤šä¸ªåŠŸèƒ½å­ç¨‹åºçš„ä¸­æ–­ä¾‹ç¨‹

- å®‰è£…ä¸€ä¸ªæ–°çš„int 7chä¸­æ–­ä¾‹ç¨‹ï¼Œä¸ºæ˜¾ç¤ºè¾“å‡ºæä¾›å¦‚ä¸‹åŠŸèƒ½å­ç¨‹åºï¼š
    1. æ¸…å±
    2. è®¾ç½®å‰æ™¯è‰²
    3. è®¾ç½®èƒŒæ™¯è‰²
    4. å‘ä¸Šæ»šåŠ¨ä¸€è¡Œ
- å…¥å£å‚æ•°è¯´æ˜å¦‚ä¸‹ï¼š
    1. ç”¨AHå¯„å­˜å™¨ä¼ é€’åŠŸèƒ½å·ï¼š  
        0 æ¸…å±
        1 è®¾ç½®å‰æ™¯è‰²
        2 è®¾ç½®èƒŒæ™¯è‰²
        3 å‘ä¸Šæ»šåŠ¨ä¸€è¡Œ  
    2. å¯¹äº1ã€2å·åŠŸèƒ½ï¼Œç”¨ALä¼ é€é¢œè‰²å€¼ï¼ŒALâˆˆ{0,1,2,3,4,5,6,7}
- è¿™é‡Œéœ€è¦æ³¨æ„åç§»åœ°å€çš„é—®é¢˜:  
    - å°†ä¸­æ–­ä¾‹ç¨‹æ”¾åœ¨æœ€å‰é¢  
    - å…¥å£åœ°å€æ”¹ä¸º20:0è€Œä¸æ˜¯0:200
    - è¿™é‡Œä¹Ÿå¯ä»¥ä½¿ç”¨orgä¼ªæŒ‡ä»¤ï¼Œæ›´ç®€ä¾¿

```x86asm
assume cs:code

stack segment
    dw 20h dup (0)
stack ends

code segment

int7c:
    jmp short int7cBegin

    table dw cls,setFgColor,setBgColor,scrollUp

    int7cBegin:
        push bx

        cmp ah,3
        ja int7cRet
        
        mov bl,ah
        mov bh,0
        add bx,bx
        call word ptr table[bx]

    int7cRet:
        pop bx
    iret

    cls:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,0
        mov cx,2000
    clsLoop:
        mov byte ptr es:[bx],' '
        add bx,2
        loop clsLoop

        pop es
        pop cx
        pop bx
    ret

    setFgColor:
        push bx
        push cx
        push es

        mov bx,0b800h
        mov es,bx
        mov bx,1
        mov cx,2000
    setFgLoop:
        and byte ptr es:[bx],11111000b
        or byte ptr es:[bx],al
        add bx,2
        loop setFgLoop

        pop es
        pop cx
        pop bx
    ret
    
    setBgColor:
        push bx
        push cx
        push es
        
        mov cl,4
        shl al,cl

        mov bx,0b800h
        mov es,bx
        mov bx,1
        mov cx,2000
    setBgLoop:
        and byte ptr es:[bx],10001111b
        or byte ptr es:[bx],al
        add bx,2
        loop setBgLoop

        pop es
        pop cx
        pop bx
    ret
    
    scrollUp:
        push si
        push di
        push ds
        push es
        push ax
        push cx

        mov ax,0b800h
        mov ds,ax
        mov si,160
        mov es,ax
        mov di,0

        cld
        mov cx,24
    scrollUpLoop:
        push cx
        mov cx,160
        rep movsb
        pop cx
        loop scrollUpLoop

        mov cx,80
    clear:
        mov byte ptr es:[di],' '
        add di,2
        loop clear

        pop cx
        pop ax
        pop es
        pop ds
        pop di
        pop si
    ret

int7cEnd:
    nop

start:
    mov ax,stack
    mov ss,ax
    mov sp,20h

    push cs
    pop ds
    mov si,int7c
    mov ax,20h
    mov es,ax
    mov di,0

    mov cx,int7cEnd-int7c
    cld
    rep movsb

    ;install int 7ch
    mov ax,0
    mov es,ax
    mov word ptr es:[4*7ch],0
    mov word ptr es:[4*7ch+2],20h

    ;Test int 7ch
    mov ah,0
    int 7ch

    mov ah,1
    mov al,2
    int 7ch

    mov ah,2
    mov al,4
    int 7ch

    mov ah,3
    int 7ch
    
    mov ax,4c00h
    int 21h
    
code ends

end start
```

## å®éªŒ 17 ç¼–å†™åŒ…å«å¤šä¸ªåŠŸèƒ½å­ç¨‹åºçš„ä¸­æ–­ä¾‹ç¨‹

- å®‰è£…ä¸€ä¸ªæ–°çš„int 7chä¸­æ–­ä¾‹ç¨‹ï¼Œå®ç°é€šè¿‡é€»è¾‘æ‰‡åŒºå·å¯¹è½¯ç›˜è¿›è¡Œè¯»å†™ã€‚  
    å‚æ•°è¯´æ˜ï¼š
    1. ç”¨AHå¯„å­˜å™¨ä¼ é€’åŠŸèƒ½å·ï¼š0è¡¨ç¤ºè¯» 1è¡¨ç¤ºå†™ï¼›
    2. ç”¨DXå¯„å­˜å™¨ä¼ é€’è¦è¯»å†™çš„æ‰‡åŒºçš„é€»è¾‘æ‰‡åŒºå·(0-2879)ï¼›
    3. ç”¨ES:BXæŒ‡å‘å­˜å‚¨è¯»å‡ºæ•°æ®æˆ–å†™å…¥æ•°æ®çš„å†…å­˜åŒºã€‚
- é‡åˆ°çš„Bugï¼š  
    å› ä¸ºæ˜¯ç›´æ¥å¤åˆ¶ç”¨çš„[å®éªŒ 16](#toc-heading-18)çš„æ¡†æ¶  
    å…¶ä¸­ç¬¬15è¡Œå¼€å§‹ï¼Œä¿®æ”¹äº†BXçš„å€¼ï¼Œè€ŒBXåˆæ˜¯int 7chçš„å‚æ•°ï¼Œä½†æ˜¯æ²¡æœ‰åœ¨è°ƒç”¨å­ç¨‹åºä¹‹å‰æ¢å¤å…¶å€¼  
    å¯¼è‡´å‘è½¯ç›˜å†™å…¥æ•°æ®çš„æ—¶å€™ï¼Œåç§»åœ°å€ä¸å¯¹ï¼Œä¼šå°‘å†™ä¸¤ä¸ªå­—èŠ‚çš„æ•°æ®ã€‚  
    è¿˜æ˜¯è¦æ³¨æ„ä¿æŠ¤å¯„å­˜å™¨çš„å€¼å•Šï¼

```x86asm
assume cs:code

data segment
    db 'Test int7ch:read from floppy sectors.','$'
data ends

backup segment
    db 512 dup (0)
backup ends

stack segment
    dw 20h dup (0)
stack ends

code segment

start:
    mov ax,stack
    mov ss,ax
    mov sp,20h

    push cs
    pop ds
    mov si,int7c
    mov ax,0
    mov es,ax
    mov di,200h

    mov cx,int7cEnd-int7c
    cld
    rep movsb

    ;install int 7ch
    mov ax,0
    mov es,ax
    mov word ptr es:[4*7ch],200h
    mov word ptr es:[4*7ch+2],0

    ;test int 7ch

    ;backup sector 1024
    mov ax,backup
    mov es,ax

    mov ah,0
    mov dx,1024
    mov bx,0
    int 7ch
    
    push es
    ;test begin
    mov ax,data
    mov es,ax
    mov ds,ax

    mov ah,2
    mov bh,0
    mov dh,8
    mov dl,30
    int 10h
    mov ah,9
    mov dx,0
    int 21h

    mov ah,1
    mov dx,1024
    mov bx,0
    int 7ch

    ;change data
    mov byte ptr es:[0],'!'

    mov ah,0
    mov dx,1024
    mov bx,0
    int 7ch

    mov ah,2
    mov bh,0
    mov dh,12
    mov dl,30
    int 10h
    mov ah,9
    mov dx,0
    int 21h

    ;test end
    pop es

    ;recovery sector 1024
    mov ah,1
    mov dx,1024
    mov bx,0
    int 7ch

    mov ax,4c00h
    int 21h

    org 200h
int7c:
    jmp short int7cBegin

    table dw rSector,wSector

    int7cBegin:
        push si
        
        cmp ah,1
        ja int7cRet

        push bx
        mov bl,ah
        mov bh,0
        add bx,bx
        mov si,bx
        pop bx
        call word ptr table[si]

    int7cRet:
        pop si
    iret

    rSector:
        push ax
        push cx
        push dx

        call getPhy
        mov ah,2
        mov al,1
        int 13h

        pop dx
        pop cx
        pop ax
    ret

    wSector:
        push ax
        push cx
        push dx

        call getPhy
        mov ah,3
        mov al,1
        int 13h

        pop dx
        pop cx
        pop ax
    ret

    getPhy:
        mov ax,dx
        mov dx,0
        mov cx,1440
        div cx
        push ax ;storage int(dx/1440)
        
        mov ax,dx
        mov cl,18
        div cl

        mov ch,al
        mov cl,ah
        inc cl

        pop dx
        mov dh,dl
        mov dl,0
    ret

int7cEnd:
    nop

code ends

end start
```

## è¯¾ç¨‹è®¾è®¡ 2

- è¦æ±‚ä¸åˆ—äº†,æœ€åè¿˜æ˜¯çƒ‚å°¾äº†ğŸ˜­
- å®Œæˆæƒ…å†µ
    - åŠŸèƒ½1ã€2
    - åŠŸèƒ½3çš„ESCé€€å‡ºåŠŸèƒ½ç‚¹æ²¡æœ‰å®Œæˆ  
        æŒ‰ESCä¼šé€ æˆä¸¥é‡é”™è¯¯ä»è€Œç›´æ¥é€€å‡ºè™šæ‹Ÿæœº  
        æ‰¾äº†å¾ˆä¹…çš„Bugéƒ½æ²¡ä»€ä¹ˆå¤´ç»ª
    - åŠŸèƒ½4æ²¡æœ‰å¯¹è¾“å…¥è¿›è¡Œå¿…è¦çš„éªŒè¯  
        äº¤äº’ä¸å¤Ÿå‹å¥½
- è™½ç„¶å¤§éƒ¨åˆ†éƒ½æ˜¯ä¹‹å‰å·²ç»å†™å¥½çš„ç¨‹åºï¼Œä½†è¿˜æ˜¯é‡åˆ°äº†å¾ˆå¤šé—®é¢˜ã€‚å…¶ä¸­å€¼å¾—è®°å½•çš„æœ‰
    - æ­å»ºç¨‹åºæ¡†æ¶æ—¶ï¼Œæ­»æ´»è¯»ä¸åˆ°ç¨‹åº  
    åŸå› æ˜¯å¯¹ä¼ªæŒ‡ä»¤org 7c00hçš„è¯¯è§£  
    ç¼–è¯‘å™¨å®é™…ä¸Šä¼šæŠŠä»£ç æ”¾åˆ°csæ®µçš„7c00hå¤„ï¼Œè€Œä¸æ˜¯ä»…ä»…è®¡ç®—æ—¶å€™ä»¥7c00hä¸ºåç§»  
    æŠŠorg 7c00hæ”¾åˆ°bootä¹‹åï¼Œå¯¼è‡´8è¡Œè®¡ç®—mov bxï¼Œbootæ—¶å€™ï¼Œå®é™…æ˜¯ä¸€ä¸ªå¾ˆå°çš„å€¼
    - ç¼–å†™åŠŸèƒ½2æ—¶ï¼Œæ­»æ´»æ²¡æ³•é‡å¯ï¼Œç›´æ¥è·‘å´©  
    ä¸€åº¦æ€€ç–‘æ˜¯ç”µè„‘å‡ºäº†é—®é¢˜ï¼Œç»è¿‡å¾ˆä¹…çš„debugï¼Œç»ˆäºæ‰¾åˆ°äº†åŸå›   
    SSè®¾ç½®åœ¨äº†7c00hä¹‹åï¼Œè€Œç³»ç»Ÿå¼•å¯¼ç¨‹åºä¾èµ–äºbiosè®¾ç½®çš„stack  
    è¿™æ ·ä¼šå¯¼è‡´æ ˆåŒºå’Œä»£ç é‡åˆï¼Œè¿›è€Œä¿®æ”¹ç³»ç»Ÿå¼•å¯¼ç¨‹åºçš„ä»£ç 

```x86asm
assume cs:code

code segment

start:
    mov ax,cs
    mov es,ax
    mov bx,boot

    mov ah,3
    mov al,1
    mov ch,0
    mov cl,1
    mov dh,0
    mov dl,0
    int 13h

    mov ax,cs
    mov es,ax
    mov bx,bootLoader

    mov ah,3
    mov al,3
    mov ch,0
    mov cl,2
    mov dh,0
    mov dl,0
    int 13h

    mov ax,4c00h
    int 21h

    org 7c00h
boot:
    mov ax,0
    mov ss,ax
    mov sp,7c00h

    mov ax,0
    mov ds,ax

    mov ax,0
    mov es,ax
    mov bx,7e00h

    mov ah,2
    mov al,3
    mov ch,0
    mov cl,2
    mov dh,0
    mov dl,0
    int 13h
    
    mov bx,0
	push bx
	mov bx,7e00h 
	push bx
	retf

    org 7e00h
bootLoader:
    jmp bootStart

    option1 db '1) reset pc    ',0
    option2 db '2) start system',0
    option3 db '3) clock       ',0
    option4 db '4) set clock   ',0
    buffer  db '                '
    tip     db 'Press F1 to change color.ESC return menu',0
    menu dw resetPC,startSys,clock,setClock
    dataPos: db 9,8,7,4,2,0
    top dw 0
;###########################################################################
bootStart:
    call showMenu
    call bootFun

    mov ax,4c00h
    int 21h
;###########################################################################
showMenu:
    push ax
    push bx
    push cx
    push dx

    mov dl,2
    call cls

    mov ax,0    
    mov dh,10
    mov dl,32
    mov cl,2
    mov si,offset option1
menuDetail:
    call show_str
    add si,10h
    inc dh
    inc ax
    cmp ax,4
jne menuDetail

    mov ah,2
    mov bh,0
    mov dh,14
    mov dl,32
    int 10h

    pop dx
    pop cx
    pop bx
    pop ax
ret
;###########################################################################
bootFun:
    call clearBuff

    mov dh,14
    mov dl,32 
    mov si,offset buffer
    call getStr

    mov si,offset buffer
    call transDigit
    mov top,0
    cmp dx,0
    jne bootStart   ;dx!=0 input illegal


    dec al          ;else  legall
    cmp al,4
    ja bootFun

    mov bl,al
    mov bh,0
    add bx,bx
    jmp word ptr menu[bx]
;---------------------------------------------------------------------------
resetPC:
    mov bx,0ffffh
    push bx
    mov bx,0
    push bx
    retf
;---------------------------------------------------------------------------
startSys:
    mov ah,2
    mov bh,0
    mov dh,0
    mov dl,0
    int 10h

    mov dl,7
    call cls

    mov ax,0
    mov es,ax
    mov bx,7c00h

    mov ah,2
    mov al,1
    mov ch,0
    mov cl,1
    mov dh,0
    mov dl,80h
    int 13h

    mov bx,0
    push bx
    mov bx,7c00h
    push bx
    retf
;---------------------------------------------------------------------------
clock:
    push ax
    push bx
    push cx
    push dx
    push si

    call instInt9
    mov dl,2
    call cls

    mov ax,0    
    mov dh,13
    mov dl,20
    mov cl,2
    mov si,offset tip
    call show_str

    mov ah,2
    mov bh,0
    mov dh,14
    mov dl,32
    int 10h

showTimeLoop:
    call showTime
jmp showTimeLoop

clockRet:
    call recInt9
    jmp bootStart
;---------------------------------------------------------------------------
setClock:
    push cx
    mov dl,2
    call cls

    call showTime
    
    mov cx,0
setClockLoop:
    call setField
    inc cx
    cmp cx,6
jne setClockLoop
    
    pop cx
    jmp bootStart
    
ret
setField:
    push ax
    push bx
    push cx
    push dx

    mov ah,2
    mov bh,0
    mov dh,12
    mov dl,32
    add dl,cl
    add dl,cl
    add dl,cl
    int 10h

    mov si,offset buffer
    call getStr

    mov si,offset buffer
    mov ah,ds:[si]
    sub ah,30h
    inc si
    mov al,ds:[si]
    sub al,30h
    mov top,0

    and ah,00001111b
    push cx
    mov cl,4
    shl ah,cl
    pop cx
    and al,00001111b
    or al,ah

    push ax
    mov si,offset dataPos
    add si,cx
    mov al,ds:[si]
    out 70h,al
    pop ax
    out 71h,al

setFieldRet:
    pop dx
    pop cx
    pop bx
    pop ax
ret
;###########################################################################
getStr:
    push ax

    call clearBuff
getStrLoop:
    mov ax,top
    cmp ax,2
    je enterKey
    mov ah,0
    int 16h
    cmp al,20h
    jb notChar

    mov ah,0
    call charFun
    mov ah,2
    call charFun

    jmp getStrLoop
notChar:
    cmp ah,0eh
    je backKey
    cmp ah,1ch
    je enterKey
    jmp getStrLoop
backKey:
    mov ah,1
    call charFun
    mov ah,2
    call charFun
    jmp getStrLoop
enterKey:
    mov al,0
    mov ah,0
    call charFun
    mov ah,2
    call charFun

    pop ax
ret

;ah 0 pushChar 1 popChar 2 showChar
charFun:
    jmp short charInit

    table dw pushChar,popChar,showChar

charInit:
    push bx
    push dx
    push di
    push es

    cmp ah,2
    ja charFunRet

    mov bl,ah
    mov bh,0
    add bx,bx
    jmp word ptr table[bx]
    
pushChar:
    mov bx,top
    mov [si][bx],al
    inc top
    jmp charFunRet

popChar:
    cmp top,0
    je charFunRet
    dec top
    mov bx,top
    mov al,[si][bx]
    jmp charFunRet

showChar:
    mov bx,0b800h
    mov es,bx
    mov al,160
    mov ah,0
    mul dh
    mov di,ax
    add dl,dl
    mov dh,0
    add di,dx

    mov bx,0
showCharLoop:
    cmp bx,top
    je isEmpty
    mov al,[si][bx]
    mov es:[di],al
    mov byte ptr es:[di+2],' '
    inc bx
    add di,2
    jmp showCharLoop
    
isEmpty:
    mov byte ptr es:[di],' '
charFunRet:
    pop es
    pop di
    pop dx
    pop bx
ret
;###########################################################################
;dx=0 success dx=1 fail 
transDigit:
    push bx
    push cx
    pushf

    mov ax,0
    mov cx,10
    mov dx,0
    push ax
    popf
transLoop:
    mov bl,byte ptr ds:[si]
    cmp bl,0
    je transRet
    cmp bl,39h
    ja transFail
    cmp bl,30h
    jb transFail
    sub bl,30h
    mul cx
    jb transFail    ;cf=1
    mov bh,0
    add ax,bx
    jb transFail    ;cf=1
    inc si
jmp transLoop

transFail:
    mov dx,1
transRet:
    popf
    pop cx
    pop bx
ret
;###########################################################################
show_str:
    push ax
    push bx
    push cx
    push dx
    push di
    push si
    push es

    mov ax,0B800H
    mov es,ax

    mov al,2
    mul dl
    mov di,ax
    mov al,0a0h
    mul dh
    mov bx,ax
    add bx,di

    mov di,0
    mov al,cl

str_loop:
    mov cx,0
    mov cl,[si]
    jcxz ok

    mov ch,al
    mov es:[bx+di],cx

    inc si
    add di,2

    jmp short str_loop
ok:
    pop es
    pop si
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
ret
;###########################################################################
;parameter dl->foreground color
cls:
    push bx
    push cx
    push es

    mov bx,0b800h
    mov es,bx
    mov bx,0
    mov cx,2000
clsLoop:
    mov byte ptr es:[bx],' '
    inc bx
    mov byte ptr es:[bx],dl
    inc bx
    loop clsLoop

    pop es
    pop cx
    pop bx
ret
;###########################################################################
clearBuff:
    mov ah,1
	int 16h
	jz clearBuffRet
	mov ah,0
	int 16h
	jmp clearBuff
clearBuffRet:
ret
;###########################################################################
showTime:
    push si
    push di
    push bx
    push es
    push cx
    push ax

    mov si,offset dataPos

    mov bx,0b800h
    mov es,bx
    mov di,160*12+32*2

    mov cx,6
    nextField:
    push cx

    mov al,ds:[si]
    inc si
    out 70h,al
    in al,71h

    mov ah,al
    mov cl,4
    shr ah,cl
    and al,0fh

    add ah,30h
    add al,30h

    mov es:[di],ah
    add di,2
    mov es:[di],al
    add di,2

    pop cx

    cmp cx,1
    je doNotShow

    cmp cx,4
    mov ah,32   ;ASCII 32 (space)
    je show
    mov ah,47   ;ASCII 47 /
    ja show
    mov ah,58   ;ASCII 58 :
    jb show
    show:
    mov es:[di],ah
    add di,2

    doNotShow:
    loop nextField
    
    pop ax
    pop cx
    pop es
    pop bx
    pop di
    pop si
ret
;###########################################################################
instInt9:
    push ds
    push ax
    push bx
    push cx
    push dx
    push di

    push cs
    pop ds

    mov ax,0
    mov es,ax
    mov di,200h

    push es:[4*9]
    pop es:[di]
    add di,2
    push es:[4*9+2]
    pop es:[di]
    add di,2

    mov si,newInt9
    mov cx,newInt9End-newInt9
    cld
    rep movsb

    cli
    mov word ptr es:[4*9],204h
    mov word ptr es:[4*9+2],0
    sti

    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    pop ds
ret

recInt9:
    push ax
    push es

    mov ax,0
    mov es,ax

    cli
    push es:[200h]
    pop es:[4*9]
    push es:[202h]
    pop es:[4*9+2]
    sti

    pop es
    pop ax
ret

newInt9:
    push ax
    push bx
    push cx
    push es

    in al,60h

    pushf
    call dword ptr cs:[200h]

    cmp al,10000001b    ;ESC
    je escFun
    cmp al,10111011b    ;F1
    je f1Fun

    jmp int9Ret

escFun:
    pop es
    pop cx
    pop bx
    pop ax
    add sp,4
    popf
    jmp clockRet

f1Fun:
    mov ax,0b800h
    mov es,ax
    mov bx,1
    mov cx,2000
    f1FunLoop:
        inc byte ptr es:[bx]
        add bx,2
    loop f1FunLoop

int9Ret:
    pop es
    pop cx
    pop bx
    pop ax
    iret
newInt9End:
    nop
;###########################################################################
code ends

end start
```

## å°¾å£°

- å¿ƒå¿ƒå¿µå¿µçš„"æ±‡ç¼–è¯­è¨€ ç‹çˆ½"ç»ˆäºå¿«è¯»å®Œäº†ï¼Œæ€»çš„æ¥è¯´éš¾åº¦ä¸é«˜ï¼Œè¯»èµ·æ¥ä¹ŸæŒºæœ‰è¶£
- ä½“éªŒäº†å½“å¹´çš„DOSï¼ˆè¿™ç©æ„å¹´é¾„æ¯”æˆ‘è¿˜å¤§ :joy:
    > MS-DOS 6.22 1994å¹´6æœˆï¼Œæœ€åä¸€ä¸ªé”€å”®ç‰ˆæœ¬
- ä½“ä¼šåˆ°è½¯ä»¶å·¥ç¨‹åŒ–çš„å¿…è¦æ€§ä»¥åŠå•å…ƒæµ‹è¯•ã€è‡ªåŠ¨åŒ–æµ‹è¯•çš„é‡è¦æ€§
- æœ€åè¿˜æ˜¯æœ‰ç¼ºæ†¾ï¼Œè¯¾ç¨‹è®¾è®¡2æ²¡æœ‰å…¨éƒ¨å®Œæˆï¼ˆä»¥ååº”è¯¥ä¼šé‡å†™
- å¦å¼€ä¸€ç¯‡ç»¼åˆç ”ç©¶
